buildscript {
    ext.kotlin_version = '1.3.61'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.16"
    }
}

apply(plugin: 'java')
apply(plugin: 'kotlin')
apply(plugin: 'maven-publish')
apply(plugin: 'java-library')
apply(plugin: 'org.jetbrains.dokka')

version = '1.1.0'
group = 'net.framework.api.rest'

repositories {
    mavenCentral()
}

sourceSets {
    main.java.srcDirs += 'src/main/java'
    main.kotlin.srcDirs += 'src/main/kotlin'
    samples.kotlin.srcDirs += 'samples/kotlin'
    samples.java.srcDirs += 'samples/java'
}

dependencies {
    // Logback
    compile 'ch.qos.logback:logback-classic:1.2.3'

    /**
     * Kotlin standard library
     */
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    /**
     * GSON
     */
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    /**
     * Mock Server
     */
    testCompile group: 'org.mock-server', name: 'mockserver-netty', version: '5.10.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    compile group: 'org.mock-server', name: 'mockserver-junit-jupiter', version: '5.10.0'
}

publishing {
    repositories {
        maven {
            name = "github"
            url = "https://maven.pkg.github.com/simonNozaki/rest-api-framework"
            credentials {
                username = findProperty("GITHUB_USER")
                password = findProperty("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = 'net.framework.api.rest'
            artifactId = 'rest-api-framework'
            version = '1.1.0'
            from(components.java)
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
        verbose = true
    }
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
}

wrapper {
    gradleVersion('6.0')
}

test {
    useJUnitPlatform()
}

processResources.destinationDir = compileJava.destinationDir
processResources.dependsOn(compileJava)

dokka {
    outputDirectory = 'docs'
}